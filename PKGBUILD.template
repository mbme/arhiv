# Maintainer: mbme

pkgname=typed-v
pkgrel=1
pkgver="0.0.0"
pkgdesc='Personal computing environment'
arch=(x86_64)
license=(MIT)
depends=(sqlite zstd libwebp)
makedepends=(git cargo npm)
options=(!buildflags !debug) # some of the build flags seem to break linking with native libs sqlite & libwebp

pkgver() {
  cd ..

  git describe --abbrev=0 --tags
}

prepare() {
  cd ..

  npm ci
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ..

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  export TYPED_V_VERSION=$(git describe --tags)

  cargo build --frozen --release --features production-mode -p binutils --bin arhiv --bin mb-scraper
}

check() {
  cd ..

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  cargo test --locked --release --features production-mode
}

package() {
  cd ..

  # scraper
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/mb-scraper"

  # arhiv
  install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/arhiv"

  install -Dm 644 "binutils/resources/v.arhiv.ui.desktop" "${pkgdir}/usr/share/applications/v.arhiv.ui.desktop"
  install -Dm 755 "binutils/resources/arhiv-server.service" "${pkgdir}/usr/lib/systemd/user/arhiv-server.service"

  mkdir -p "target/completions"
  "target/release/arhiv" generate-completions bash > "target/completions/arhiv.bash"
  "target/release/arhiv" generate-completions zsh > "target/completions/_arhiv"

  install -Dm 755 "target/completions/arhiv.bash" "${pkgdir}/usr/share/bash-completion/completions/arhiv.bash"
  install -Dm 755 "target/completions/_arhiv" "${pkgdir}/usr/share/zsh/site-functions/_arhiv"
}
