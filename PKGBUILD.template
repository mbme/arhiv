# Maintainer: mbme

pkgname=arhiv
pkgrel=1
pkgver="0.0.0"
pkgdesc='Personal computing environment'
arch=(x86_64)
license=(MIT)
depends=(dbus)
optdepends=(
    'electron34: to run Arhiv desktop app'
)
makedepends=(git cargo lld nodejs npm)
options=(!buildflags !debug) # some of the build flags seem to break linking with native libs sqlite & libwebp

export RUSTUP_TOOLCHAIN=stable
export CARGO_TARGET_DIR=target

pkgver() {
  cd ..

  just print-version
}

prepare() {
  cd ..

  npm ci
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ..

  just prod-build
  just prod-build-desktop
}

check() {
  cd ..

  just check
}

package() {
  cd ..

  # arhiv CLI
  install -Dm 755 -t "$pkgdir/usr/bin/" "target/release/arhiv"

  # arhiv desktop app
  install -Dm 755 "arhiv-desktop/arhiv-desktop" "$pkgdir/usr/bin/arhiv-desktop"
  install -Dm 644 "arhiv-desktop/dist/index.cjs" "$pkgdir/usr/share/arhiv-desktop/dist/index.cjs"
  install -Dm 644 "arhiv-desktop/electron-package.json" "$pkgdir/usr/share/arhiv-desktop/package.json"

  install -Dm 644 "binutils/resources/v.arhiv.ui.desktop" "${pkgdir}/usr/share/applications/v.arhiv.ui.desktop"

  mkdir -p "target/completions"
  "target/release/arhiv" generate-completions bash > "target/completions/arhiv.bash"
  "target/release/arhiv" generate-completions zsh > "target/completions/_arhiv"

  install -Dm 755 "target/completions/arhiv.bash" "${pkgdir}/usr/share/bash-completion/completions/arhiv.bash"
  install -Dm 755 "target/completions/_arhiv" "${pkgdir}/usr/share/zsh/site-functions/_arhiv"
}
